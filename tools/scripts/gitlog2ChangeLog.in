#! @PERL@

#
# gitlog2ChangeLog.in
#
# Copyright (C) 2007       Francesco Salvestrini
# Copyright (C) 2002, 2006 Emmanuele Bassi
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License along
# with this program; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
#

use strict;
use warnings;

use POSIX qw( strftime );

my $program        = "gitlog2ChangeLog";

our $GIT_LOG       = 'git-log --pretty=format:"%at:%an:<%ae>:%h:%s"';
our $GIT_DIFF_TREE = 'git-diff-tree --name-only -r';

our $revs = $ARGV[0] or undef;

my $log_cmd = $GIT_LOG;
$log_cmd .= ' ' . $revs if defined $revs;

my $git_log;
if (!open($git_log, '-|', $log_cmd)) {
    print STDERR "$program: Unable to invoke git-log ($!)\n";
    exit 1;
}

while (<$git_log>) {
    my $log_line = $_;

    chomp($log_line);

    my ($timestamp, $committer, $email, $commit_hash, $subject) =
        split(/:/, $log_line, 5);
    
    # use a shorter date line
    my $date = strftime("%Y-%m-%d", localtime($timestamp));

    print $date, "  ", $committer, "  ", $email, "\n\n";

    # list the file changes
    if ($commit_hash) {
        my $cmd = $GIT_DIFF_TREE . " " . $commit_hash;

	my $git_diff;
        if (!open($git_diff, '-|', $cmd)) {
            print STDERR "$program: Unable to invoke git-diff-tree ($!)\n";
	    exit 1;
	}

        while (<$git_diff>) {
            my $diff_line = $_;

            chomp($diff_line);

            next if $diff_line =~ /^$commit_hash/;
            print "\t* ", $diff_line, ":\n";
        }

        close($git_diff);
    }
    else {
        print "\t* *:\n";
    }

    print "\n";

    # no need to use the full body, the subject will do
    print "\t", $subject, "\n" if $subject;

    print "\n";
}

close($git_log);

exit 0;
