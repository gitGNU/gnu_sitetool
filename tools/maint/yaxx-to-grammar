#!/usr/bin/perl

#
# yaxx-to-grammar
#
# Copyright (C) 2007, 2008 Francesco Salvestrini
#                          Alessandro Massignan
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License along
# with this program; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
#

use strict;
use warnings;
use diagnostics;

use Getopt::Long;
use Text::Balanced;

(my $ME = $0) =~ s|.*/||;
my $VERSION = "0.0.1";

sub usage ($)
{
    my $message   = shift;
    my $exit_code = ($message eq "" ? 0 : 1);
    my $STREAM    = ($exit_code == 0 ? *STDOUT : *STDERR);

    if ($exit_code != 0) {
	print $STREAM "Try `$ME --help' for more information.\n";
    } else {
	print $STREAM <<EOF;
Usage: $ME [OPTIONS] INFILE OUTFILE

Extract grammar from a yacc/bison/yapp parser description

OPTIONS:

    --help       display this help and exit
    --version    output version information and exit

Report bugs to <salvestrini\@gmail.com>
EOF
    }

    exit $exit_code;
}

sub fatal ($)
{
    my $string = shift;
    print "$ME: $string\n";
    exit(1);
}

{
    GetOptions(
	help    => sub {
	    usage("");
	},
	version => sub {
	    print <<EOF;
$ME ($VERSION)

Copyright (C) 2007, 2008 Francesco Salvestrini
			 Alessandro Massignan

This is free software.  You may redistribute copies of it under the terms of
the GNU General Public License <http://www.gnu.org/licenses/gpl.html>.
There is NO WARRANTY, to the extent permitted by law.
EOF
exit(0);
	},
	) or usage("Unrecognized option");

    defined($ARGV[0]) or usage("Missing input file!");
    defined($ARGV[1]) or usage("Missing output file!");

    # Slurp the whole input file
    my $in_fh;
    open($in_fh, "<", $ARGV[0]) or fatal($!);

    local $/;

    my $input = <$in_fh>;
    close $in_fh;

    $input =~ s/^\s*//;
    $input =~ s/\s*$//;

    # Only declarations and grammar sections are required
    my $declarations;
    my $grammar;

    if (!(($input =~ m/^(.*)\s%%\s(.*)\s%%\s.*$/s) ||
	  ($input =~ m/^(.*)\s%%\s(.*)/s))) {
	fatal("Wrong input file format");
    }
    $declarations = $1;
    $grammar      = $2;

    defined($grammar) or fatal("Wrong grammar section in input file ?");

    # Handle declarations, extract the start symbol
    my $start;

    $declarations =~ /\s*\%start\s+(.*)\s*\n/;
    $start = $1;

    defined($start) or fatal("Missing start symbol in declarations");

    # Begin parsing and cleaning
    my $output;

    $output = "";
    while (($grammar =~ s/^([^\'\{\#\;]+)//) ||
	   ($grammar =~ /^[\'\{\#\;]/))       {
	my $string;
	my $next_token;

	# Clean up string and append it to output
	if (defined($1) && ($1 !~ /^\s*$/)) {

	    $string = $1;
	    $string =~ s/^\s*//;
	    $string =~ s/\s*$//;
	    $string =~ s/\s+/\ /g;

	    if (($output eq "") || ($output =~ /\n$/)) {
		$output = $output . $string;
	    } else {
		$output = $output . " " . $string;
	    }
	}

	if ($grammar !~ /^\s*$/) {
	    $grammar    =~ /^(.)/;
	    $next_token = $1;
	} else {
	    last;
	}

	if ($next_token eq '\'') {
	    # Preserve single-quoted string

	    my $extracted;
	    my $remainder;

	    ($extracted, $remainder) =
		Text::Balanced::extract_quotelike($grammar);

	    $output  = $output . " " . $extracted;
	    $grammar = $remainder;
	} elsif ($next_token eq '{') {
	    # Wipe out semantic action

	    my $extracted;
	    my $remainder;

	    ($extracted, $remainder) =
		Text::Balanced::extract_bracketed($grammar, '{}');

	    $grammar = $remainder;
	} elsif ($next_token eq '#') {
	    # Remove comments

	    $grammar =~ s/^\#[^\n]*//;
	} elsif ($next_token eq ';') {
	    # Handling rule's end

	    $grammar =~ s/^;\s*//;
	    $output = $output . " ;\n";
	} else {
	    fatal("Bug hit!");
	}
    }
    $output = "Start symbol " . $start . "\n\n" . $output;

    # Finally write the output
    my $out_fh;
    open($out_fh, ">", $ARGV[1]) or fatal($!);
    print $out_fh $output;
    close($out_fh);
}

exit 0;
