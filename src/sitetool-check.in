#! @PERL@

#
# sitetool-check
#
# Copyright (C) 2007, 2008 Francesco Salvestrini
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License along
# with this program; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
#

eval 'case $# in 0) exec @PERL@ -S "$0";; *) exec @PERL@ -S "$0" "$@";; esac'
    if 0;

use File::Spec;

BEGIN
{
    # Retrieve our perl libraries path
    my $perllibdir;

    $perllibdir = defined($ENV{'SITETOOL_LIBRARY_PATH'}) ?
	$ENV{'SITETOOL_LIBRARY_PATH'} : '@pkgvlibsdir@';

    unshift(@INC, map(File::Spec->catfile($_, "perl"),
		      (split '@PATH_SEPARATOR@', $perllibdir)));

    # Override SHELL.  On DJGPP SHELL may not be set to a shell
    # that can handle redirection and quote arguments correctly,
    # e.g.: COMMAND.COM.  For DJGPP always use the shell that configure
    # has detected.
    $ENV{'SHELL'} = '@SHELL@' if ($^O eq 'dos');
}

#
# Misc
#

use warnings;
use strict;
use diagnostics;

use File::Path;

use Sitetool::Autoconfig;
use Sitetool::Base::Trace;
use Sitetool::Base::Debug;
use Sitetool::Base::Program;
use Sitetool::Base::Options;
use Sitetool::OS::Shell;
use Sitetool::OS::Directory;
use Sitetool::OS::Filename;
use Sitetool::OS::File;
use Sitetool::WWW::HREF;
use Sitetool::WWW::HTTP;
use Sitetool::WWW::HTTPS;
use Sitetool::WWW::FTP;
use Sitetool::WWW::MAILTO;
use Sitetool::Configuration;
use Sitetool::Array;

#
# Globals
#

our $PROGRAM_NAME = "sitetool-check";

#
# Utilities
#

sub help_brief ()
{
    print "  -o, --output-dir=DIR        use DIR as output directory\n";
    print "  -f, --force                 consider all files obsolete\n";
}

sub help_common ()
{
    print "  -W, --warnings=CATEGORY     report the warnings falling in CATEGORY\n";
    print "  -d, --debug                 enable debugging mode\n";
    print "  -v, --verbose               enable verbose mode\n";
    print "  -h, --help                  print this help, then exit\n";
    print "  -B, --brief-help            print a brief help, then exit\n";
    print "  -V, --version               print version number, then exit\n";
}

sub help ()
{
    print "Usage: $PROGRAM_NAME [OPTIONS]\n";
    print "\n";
    print "  -c, --configuration=FILE    configuration file is FILE\n";

    help_brief();
    help_common();

    print "\n";
    print "Warning categories include:\n";
    print "  \`all'           all the warnings\n";
    print "  \`none'          turn off all the warnings\n";
    print "\n";
    print "Report bugs to <$PACKAGE_BUGREPORT>\n";
}

#
# Main
#

trace_prefix_set($PROGRAM_NAME);

# XXX FIXME: To be removed ASAP
my $output_dir         = "";
my $configuration_file = "";
my $force_mode         = 0;
my $dry_run            = 0;
my $run                = 1;

debug_set(0);
verbose_set(0);
warning_set("none");

my $options = Sitetool::Base::Options->new();
assert(defined($options));

my @options_list = (
    [ "c", "configuration",
      sub { $configuration_file = $_[0];                     return 1; }, 1,1 ],
    [ "o", "output-dir",
      sub { $output_dir = $_[0];                             return 1; }, 1,1 ],
    [ "n", "dry-run",    sub { $dry_run = 1;                 return 1; }, 0,0 ],
    [ "f", "force",      sub { $force_mode = 1;              return 1; }, 0,0 ],
    [ "W", "warnings",   sub { warning_set($_[0]);           return 1; }, 1,1 ],
    [ "d", "debug",      sub { debug_set(1);                 return 1; }, 0,0 ],
    [ "v", "verbose",    sub { verbose_set(1);               return 1; }, 0,0 ],
    [ "h", "help",       sub { help();             $run = 0; return 1; }, 0,0 ],
    [ "B", "help-brief", sub { help_brief();       $run = 0; return 1; }, 0,0 ],
    [ "V", "version",    sub { version();          $run = 0; return 1; }, 0,0 ],
    );

if (!$options->config(\@options_list)) {
    bug("Options configuration problem");
}

my @options_slack = @ARGV;
if(!$options->parse(\@options_slack)) {
    hint("Option parsing failed");
    exit 1;
}

debug("Running \`" . $PROGRAM_NAME . "' version \`" . $PACKAGE_VERSION . "'");

#
# Parameters check
#

if (!$run) {
    exit 0;
}

if ($configuration_file eq "") {
    hint("Missing configuration file");
    exit 1;
}
if ($output_dir eq "") {
    hint("Missing output directory");
    exit 1;
}

if ($dry_run) {
    exit 0;
}

#
# Start the real work
#

my %configuration;
if (!configuration_melt($configuration_file, \%configuration)) {
    error("Cannot melt configuration from file \`$configuration_file'");
    hint("Try forcing a complete configuration file re-parsing");
    exit 1;
}

#
# Get output directory
#
$output_dir = filename_rel2abs($output_dir);

#
# Validate each page
#
verbose("Extracting links");
my %links;
for my $page_id (keys(%{$configuration{PAGES}})) {
    verbose("Extracting page \`" . $page_id . "' links");

    my $destination;
    $destination = $configuration{PAGES}{$page_id}{DESTINATION};
    if (!defined($destination)) {
	error("Page \`" . $page_id. "' has no destination");
	exit 1;
    }

    my $file;
    $file = File::Spec->catfile($output_dir, $destination);
    assert(file_ispresent($file));

    my $string;
    $string = undef;
    if (!file_tostring($file, \$string)) {
	error("Problems reading file \`" . $file . "'");
	exit 1;
    }
    assert(defined($string));
    
    my @page_links;
    @page_links = array_uniq(href_extract($string));
    
    verbose("Page \`" . $page_id . "' links are \`@page_links'");
	
    for my $link (@page_links) {
	if (!defined($links{$link})) {
	    $links{$link} = [ $page_id ];
	} else {
	    push(@{$links{$link}}, $page_id);
	}
    }
}

#verbose("Links are");
#for my $link (keys(%links)) {
#    my @pages;
#
#    @pages = @{$links{$link}};
#    debug("Link \`" . $link . "' is present in pages \`@pages'");
#}

verbose("Checking links");
for my $link (keys(%links)) {

    my $filename;
    
    $filename = filename_mktemp();
    assert(defined($filename));
    
    my $downloaded;
    
    $downloaded = 0;
    if (href_ishttp($link)) {
	verbose("Checking http link \`" . $link . "'");
	if (http_download($link, $filename)) {
	    $downloaded = 1;
	}
    } elsif (href_ishttps($link)) {
	verbose("Checking https link \`" . $link . "'");
	if (https_download($link, $filename)) {
	    $downloaded = 1;
	}
    } elsif (href_isftp($link)) {
	verbose("Checking ftp link \`" . $link . "'");
	if (ftp_download($link, $filename)) {
	    $downloaded = 1;
	}
    } elsif (href_ismailto($link)) {
	debug("Skipping mailto link \`" . $link . "'");
	$downloaded = 1;
    } else {
	debug("Skipping local link \`" . $link . "'");
	$downloaded = 1;
    }
    
    file_remove($filename);
    
    if (!$downloaded) {
	my @involved_pages;

	@involved_pages = @{$links{$link}};

	my $err;
	
	$err =
	    "Cannot download link \`" . $link . "' " .
	    "(involved pages \`@involved_pages')";

	if ($force_mode) {
	    warning($err);
	} else {
	    error($err);
	    exit 1;
	}
    }
}

exit 0;
